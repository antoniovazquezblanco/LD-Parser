plugins {
    id 'signing'
    id 'java-library'
    id 'antlr'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    id 'com.palantir.git-version' version '4.0.0'
}

group = 'io.github.antoniovazquezblanco'
version = gitVersion()

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.2'
    implementation 'org.antlr:antlr4-runtime:4.13.2'
    testImplementation 'junit:junit:4.13.2'
}

sourceSets.configureEach {
    java.srcDir(tasks.named(getTaskName("generate", "GrammarSource")).map { files() })
}

mavenPublishing {
    coordinates(project.group, "ld-parser", project.version)
    pom {
        name = 'LD Parser'
        description = 'A Java library to parse linker script (LD) files.'
        url = 'https://github.com/antoniovazquezblanco/LD-Parser'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'antoniovazquezblanco'
                name = 'Antonio VÃ¡zquez Blanco'
                email = 'antoniovazquezblanco@gmail.com'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/antoniovazquezblanco/LD-Parser.git'
            developerConnection = 'scm:git:git@github.com:antoniovazquezblanco/LD-Parser.git'
            url = 'https://github.com/antoniovazquezblanco/LD-Parser'
        }
    }
    signAllPublications()
    publishToMavenCentral()
}
